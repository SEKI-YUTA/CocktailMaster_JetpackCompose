plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlinx-serialization'
    id 'com.google.devtools.ksp'
    id 'io.github.takahirom.roborazzi'
    id 'com.apollographql.apollo3'
}

apollo {
    service("service") {
        packageName.set("com.yuta.cocktailmaster")
    }
}

android {

    namespace 'com.yuta.cocktailmaster'
    compileSdk 34

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    defaultConfig {
        applicationId "com.yuta.cocktailmaster"
        minSdk 26
        targetSdk 34
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions "versions"
    productFlavors {
        physical {
            dimension "versions"
            applicationIdSuffix ".physical"
            buildConfigField("String", "BASE_URL", "\"https://cocktail-api.fly.dev/\"")
        }
        emulator {
            dimension "versions"
            applicationIdSuffix ".emulator"
            buildConfigField("String", "BASE_URL", "\"http://10.0.2.2:9090/\"")
        }
        gql {
            dimension "versions"
            applicationIdSuffix ".gql"
            // このフレーバーではBASE_URLは使わないが、定義しないとエラーになるのでダミーの値を入れる
            buildConfigField("String", "BASE_URL", "\"http://10.0.2.2:9090/\"")
        }
    }

    variantFilter { variant ->
        def names = variant.flavors*.name
        def buildType = variant.buildType.name
        if(names.contains("emulator") && buildType.contains("release")) {
            setIgnore(true)
        }
    }

    compileOptions {
        sourceCompatibility 17
        targetCompatibility 17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        compose true
        buildConfig true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.4.2'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    def nav_version = "2.7.5"
    def room_version = "2.5.0"
    def lifecycle_version = "2.6.2"
    def mockkVersion = "1.13.8"

    implementation 'androidx.core:core-ktx:1.12.0'
    implementation platform('org.jetbrains.kotlin:kotlin-bom:1.8.0')
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.2'
    implementation 'androidx.activity:activity-compose:1.8.1'
    implementation platform('androidx.compose:compose-bom:2022.10.00')
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-graphics'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    implementation 'androidx.compose.material3:material3'

    implementation "androidx.navigation:navigation-compose:$nav_version"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    // To use Kotlin Symbol Processing (KSP)
    ksp "androidx.room:room-compiler:$room_version"
    // ViewModel utilities for Compose
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:$lifecycle_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.5.1'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.jakewharton.retrofit:retrofit2-kotlinx-serialization-converter:1.0.0'
    // https://mvnrepository.com/artifact/org.jetbrains.kotlin/kotlin-gradle-plugin

    testImplementation 'junit:junit:4.13.2'
    testImplementation "org.robolectric:robolectric:4.11.1"
    testImplementation "io.github.takahirom.roborazzi:roborazzi:1.8.0-alpha-3"
    testImplementation "io.github.takahirom.roborazzi:roborazzi-compose:1.8.0-alpha-3"
    testImplementation "io.github.takahirom.roborazzi:roborazzi-junit-rule:1.8.0-alpha-3"
    testImplementation "io.mockk:mockk-android:${mockkVersion}"
    testImplementation "io.mockk:mockk-agent:${mockkVersion}"
    testImplementation 'androidx.compose.ui:ui-test-junit4'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation platform('androidx.compose:compose-bom:2022.10.00')
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4'
    debugImplementation 'androidx.compose.ui:ui-tooling'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'

    implementation "androidx.constraintlayout:constraintlayout-compose:1.0.1"

    implementation "com.apollographql.apollo3:apollo-runtime:3.8.2"
//    implementation "com.apollographql.apollo:apollo-coroutines-support:3.8.2"
}